fun mac/2.

free Error_6300,get_challenge.

fun senc/2.
reduc sdec(senc(x,y),y) -> x.


// Description of the reader role

let reader(c,k_e,k_m) =
  out(c,get_challenge);
  in(c,xn_t);
  new n_r;
  new k_r;
  let xm = senc((n_r,xn_t,k_r),k_e) in
  out(c,(xm,mac(xm,k_m)));
  in(c,y).

// Description of the passport role

let passport(c,k_e,k_m) =
  in(c,x);
  if x = get_challenge then
    new n_t;
    out(c,n_t);
    in(c,y);
    let (xm_e,xm_m) = y in
    if xm_m = mac(xm_e,k_m) then
      let (xn_r,xn_t,xk_r) = sdec(xm_e,k_e) in
        if xn_t = n_t then (
        	new k_t;
        	let z = senc((n_t,xn_r,k_t),k_e) in
        	out(c,(z,mac(z,k_m)))
        ) else out(c,Error_6300)
      else out(c,Error_6300)
    else out(c,Error_6300).


free c,c_p,c_r.
fun enc_key_of/1 [private].
fun mac_key_of/1 [private].

let system(id) = // two systems with the same seed use the same keys
  passport(c_p,enc_key_of(id),mac_key_of(id)) |
  reader(c_r,enc_key_of(id),mac_key_of(id)).


// 2 sessions
let dupl_system2 = new id; !^2 system(id).
let fresh_system2 = !^2 new id; system(id).
query session_incl(dupl_system2,fresh_system2).
// query trace_incl(dupl_system2,fresh_system2). // 5min 23s
query trace_equiv(dupl_system2+fresh_system2,fresh_system2). // 17min 17s

// 3 sessions
let dupl_system3 =
  in(c,ids); let (id1,id2,id3) = ids in
  (system(id1) | system(id2) | system(id3)).
let fresh_system3 =
  in(c,ids); let (id1,id2,id3) = ids in
  !^3 new id; system(id).
query session_incl(dupl_system3,fresh_system3).

// 4 sessions
let dupl_system4 =
  in(c,ids); let (id1,id2,id3,id4) = ids in
  (system(id1) | system(id2) | system(id3) | system(id4)).
let fresh_system4 =
  in(c,ids); let (id1,id2,id3,id4) = ids in
  !^4 new id; system(id).
query session_incl(dupl_system4,fresh_system4).

// 5 sessions
let dupl_system5 =
  in(c,ids); let (id1,id2,id3,id4,id5) = ids in
  (system(id1) | system(id2) | system(id3) | system(id4) | system(id5)).
let fresh_system5 =
  in(c,ids); let (id1,id2,id3,id4,id5) = ids in
  !^5 new id; system(id).
query session_incl(dupl_system5,fresh_system5).
